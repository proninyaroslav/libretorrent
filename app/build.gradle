import com.android.build.OutputFile

plugins {
    id 'com.android.application'
    id 'androidx.navigation.safeargs'
}

android {
    namespace 'org.proninyaroslav.libretorrent'
    defaultConfig {
        applicationId 'org.proninyaroslav.libretorrent'
        minSdkVersion 24
        targetSdkVersion 34
        compileSdk 35
        // Version code format: 0000xx0
        // xx - version code
        // trailing zero - auto increment for split ABI
        versionCode 9000310
        // Version format: major.minor.[revision][-some_info]
        versionName '3.6'
        vectorDrawables.useSupportLibrary true
        multiDexEnabled true

        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        }

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ['room.schemaLocation': "$projectDir/schemas".toString()]
            }
        }

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    flavorDimensions = ["deploy"]

    productFlavors {
        base {
            dimension "deploy"
        }
        play {
            dimension "deploy"
        }
    }

    buildTypes {
        /* Enable or disable logging in build types */
        def SESSION_LOGGING = 'SESSION_LOGGING'

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            buildConfigField('boolean', SESSION_LOGGING, 'false')
            signingConfig signingConfigs.debug
        }

        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
            debuggable true

            buildConfigField('boolean', SESSION_LOGGING, 'true')
            enableUnitTestCoverage true
            enableAndroidTestCoverage true
        }
    }

    splits {
        abi {
            enable true
            reset()
            include 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
            universalApk true
        }
    }

    project.ext.versionCodes = ['armeabi-v7a': 1, 'arm64-v8a': 2, 'x86': 3, 'x86_64': 4]
    applicationVariants.configureEach { variant ->
        outputs.configureEach { output ->
            versionCodeOverride =
                    project.ext.versionCodes.get(output.getFilter(OutputFile.ABI), 0) + versionCode
        }
    }

    compileOptions {
        encoding 'UTF-8'
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    lint {
        abortOnError false
    }

    buildFeatures {
        viewBinding true
        dataBinding true
        buildConfig true
    }

    testOptions {
        unitTests {
            includeAndroidResources true
        }
    }

    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
}

tasks.withType(Test).configureEach {
    testLogging {
        exceptionFormat 'full'
        events 'started', 'skipped', 'passed', 'failed'
        showStandardStreams true
    }
}

def ROOM_VERSION = '2.6.1'
def LIFECYCLE_VERSION = '2.8.7'
def NAV_VERSION = "2.8.9"
def MOSHI_VERSION = "1.15.2"
def PAGING_VERSION = "3.3.6";

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.5'

    /* Testing */
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test:rules:1.6.1'
    androidTestImplementation "androidx.room:room-testing:$ROOM_VERSION"
    testAnnotationProcessor 'com.google.auto.service:auto-service:1.1.1'

    /* AndroidX libraries */
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.fragment:fragment:1.8.6'
    implementation 'androidx.activity:activity:1.10.1'
    implementation "androidx.lifecycle:lifecycle-livedata-core:$LIFECYCLE_VERSION"
    implementation "androidx.lifecycle:lifecycle-viewmodel:$LIFECYCLE_VERSION"
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation "androidx.paging:paging-runtime:$PAGING_VERSION"
    implementation "androidx.paging:paging-rxjava2:$PAGING_VERSION"
    implementation 'androidx.recyclerview:recyclerview:1.4.0'
    implementation 'androidx.recyclerview:recyclerview-selection:1.1.0'
    implementation "androidx.room:room-rxjava2:$ROOM_VERSION"
    implementation "androidx.room:room-runtime:$ROOM_VERSION"
    annotationProcessor "androidx.room:room-compiler:$ROOM_VERSION" /* For schemas import */
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.work:work-runtime:2.10.0'
    implementation 'com.google.android.material:material:1.13.0-alpha12'
    implementation 'androidx.viewpager2:viewpager2:1.1.0'
    implementation "androidx.lifecycle:lifecycle-viewmodel:$LIFECYCLE_VERSION"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$LIFECYCLE_VERSION"
    implementation 'androidx.preference:preference:1.2.1'
    // Fix for WorkManager https://github.com/google/ExoPlayer/issues/7993
    implementation 'com.google.guava:guava:33.4.0-jre'
    implementation "androidx.navigation:navigation-fragment:$NAV_VERSION"
    implementation "androidx.navigation:navigation-ui:$NAV_VERSION"
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'

    /* ReactiveX */
    implementation 'io.reactivex.rxjava2:rxjava:2.2.21'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

    /* ACRA */
    def ACRA_VERSION = '5.12.0'
    implementation "ch.acra:acra-dialog:$ACRA_VERSION"
    implementation "ch.acra:acra-mail:$ACRA_VERSION"

    /* libtorrent wrapper */
    def LIBTORRENT4J_VERSION = '2.1.0-35'
    implementation "org.libtorrent4j:libtorrent4j:$LIBTORRENT4J_VERSION"
    implementation "org.libtorrent4j:libtorrent4j-android-arm:$LIBTORRENT4J_VERSION"
    implementation "org.libtorrent4j:libtorrent4j-android-arm64:$LIBTORRENT4J_VERSION"
    implementation "org.libtorrent4j:libtorrent4j-android-x86:$LIBTORRENT4J_VERSION"
    implementation "org.libtorrent4j:libtorrent4j-android-x86_64:$LIBTORRENT4J_VERSION"

    /* Other */
    implementation 'com.github.cachapa:ExpandableLayout:2.9.2'
    implementation 'com.github.skydoves:colorpickerview:2.3.0'
    implementation 'com.github.anthonynsimon:jurl:v0.4.2'
    /* Don't use Commons IO >= 2.6, because they uses Java NIO, supported only from API 26 */
    //noinspection GradleDependency
    implementation 'commons-io:commons-io:2.5'
    implementation 'org.apache.commons:commons-text:1.13.0'
    implementation 'com.google.code.gson:gson:2.11.0'
    implementation 'net.java.dev.jna:jna:5.16.0@aar'
    implementation "com.squareup.moshi:moshi:$MOSHI_VERSION"
    implementation "com.squareup.moshi:moshi-adapters:$MOSHI_VERSION"

    constraints {
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.8.0") {
            because("kotlin-stdlib-jdk7 is now a part of kotlin-stdlib")
        }
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.0") {
            because("kotlin-stdlib-jdk8 is now a part of kotlin-stdlib")
        }
    }
}
